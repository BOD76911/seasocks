
add_subdirectory(main/c)
add_subdirectory(main/web)

function(add_seasocks_executable NAME)
    add_library(${NAME}_objs OBJECT ${ARGN})
    target_link_libraries(${NAME}_objs seasocks_headers)
    if (TARGET seasocks)
        add_executable(${NAME} $<TARGET_OBJECTS:${NAME}_objs>)
        target_link_libraries(${NAME} PRIVATE seasocks)
    endif ()
    if (TARGET seasocks_so)
        add_executable(${NAME}_so $<TARGET_OBJECTS:${NAME}_objs>)
        target_link_libraries(${NAME}_so PRIVATE seasocks_so)
    endif ()
endfunction()

function(seasocks_link_libraries NAME)
    if (TARGET ${NAME})
        target_link_libraries(${NAME} ${ARGN})
    endif ()
    if (TARGET ${NAME}_so)
        target_link_libraries(${NAME}_so ${ARGN})
    endif ()
endfunction()

if (SEASOCKS_EXAMPLE_APP)
  add_subdirectory(app/c)
endif ()

if (UNITTESTS)
    find_program(CMAKE_MEMORYCHECK_COMMAND valgrind)
    enable_testing()
    add_subdirectory(test/c)
endif ()
